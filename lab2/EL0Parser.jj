// This is supporting software for CS321/CS322 Compilers and Language Design.
// Copyright (c) Portland State University
//
// EL0 parser specification. (For CS322, Jingke Li)
//
//
PARSER_BEGIN(EL0Parser)
import java.io.*;

public class EL0Parser {
  public static void main(String [] args) throws Exception {
    if (args.length == 1) {
    	FileInputStream stream = new FileInputStream(args[0]);
    	EL0.Exp prog = new EL0Parser(stream).Program();
    	stream.close();
    	System.out.println(prog);
    } else {
    	System.out.println("Need a file name as command-line argument.");
    }
  }
}
PARSER_END(EL0Parser)

SKIP: 
{
  " " | "\t" | "\r" | "\f" | "\n"
| <COMMENT: "#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN: 
{
  "let" | "in" | "end"
}

TOKEN: 
{
  <#Digit:   ["0"-"9"]>
| <#Letter:  ["A"-"Z"]|["a"-"z"]>
| <Num:      (<Digit>)+> 
| <Var:      <Letter> (<Letter>|<Digit>)*>
}

// Program -> Exp
//
EL0.Exp Program():
{ EL0.Exp e; }
{
  e=Exp(null) <EOF> { return e; }
}

// Exp -> Base [+ Exp]
// 
EL0.Exp Exp(EL0.Exp e0):
{ EL0.Exp e; }
{
  e=Base() { if (e0 != null) e = new EL0.Plus(e0,e); }
  [ "+" e=Exp(e) ]
  { return e; }
}

// Base -> let var = Exp in Exp end
//        |  var
//        |  num
//
EL0.Exp Base():
{ EL0.Exp e, e1, e2;
  EL0.Var var; 
  EL0.Num num; }
{
  ( "let" var=Var() "=" e1=Exp(null) "in" e2=Exp(null) "end"
    { e = new EL0.Let(var,e1,e2); }
  | e=Var()
  | e=Num() )
  { return e; }
}


EL0.Var Var():
{ Token t; }
{
  t=<Var> { return new EL0.Var(t.image); }
}

EL0.Num Num():
{ Token t; }
{
  t=<Num> { return new EL0.Num(Integer.parseInt(t.image)); }
}


